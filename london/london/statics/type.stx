module statics/type

imports statics/base
imports signatures/type-sig

rules

  declOk(s, TypeDef(x, t)) :- 
    declareType(s, x, typeOfType(s, t)).
    
  typeOfType(s, TypeRef(x)) = typeOfTypeRef(s, x).
    
  typeOfExp(s, e1@Ascr(e, t)) = T :- {S}
    typeOfExp(s, e) == S,
    typeOfType(s, t) == T,
    subtype(S, T),
    @e1.type := T.
    
signature 
  namespaces
    Type : string
  name-resolution   
    resolve Type 
     filter P* (R* | I*) 
        min $ < I, $ < P, I < P, R < P  
        
rules

  declareType : scope * string * TYPE
  typeOfTypeRef : scope * string -> TYPE
    
  declareType(s, x, T) :-   
    s -> Type{x} with typeOfDecl T,
    @x.type := T, 
    typeOfDecl of Type{x} in s |-> [(_, (_, T))] 
             | error $[Duplicate definition of type [x]].
               // declaration is distinct
  
  typeOfTypeRef(s, x) = T :- {x'}
    typeOfDecl of Type{x} in s |-> [(_, (Type{x'}, T))|_] 
             | error $[Type [x] not defined], 
               // permissive lookup to cope with double declaration 
    @x.ref := x',
    @x.type := T.
               